/*
 * Copyright (C) 2016 Jorge Ruesga
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
    libraries = [
        androidx_appcompat          : '1.2.0',
        androidx_browser            : '1.4.0',
        androidx_cardview           : '1.0.0',
        androidx_exifinterface      : '1.3.6',
        androidx_legacy             : '1.0.0',
        material                    : '1.6.0',
        play_services_auth          : '20.7.0',
        firebase_core               : '21.1.1',
        firebase_messaging          : '21.1.0',
        firebase_crashlytics        : '18.3.7',
        gson                        : '2.10.1',
        retrofit2                   : '2.9.0',
        okhttp3                     : '4.11.0',
        okio                        : '3.4.0',
        okhttp_digest               : 'f13c5b8f79',
        rxjava                      : '3.1.8',
        rxandroid                   : '3.0.2',
        rxloader2                   : 'edeebf0a1a',
        commons_io                  : '2.14.0',
        antlrv3                     : '3.5.3',
        smoothprogressbar           : '1.1.0',
        glide                       : '4.12.0',
        prettytime                  : '5.0.7.Final',
        flexboxlayout               : '3.0.0',
        floatingsearchview          : '2d435bead5',
        androidsvg                  : '1.4',
        keyboardvisibilityevent     : '2.3.0',
        better_link_movement_method : '2.2.0',
        aboutlibraries              : '8.8.6',
        aboutlibraries_definitions  : '8.8.6'
    ]

    testLibraries = [
        junit                       : '4.13.2',
        hamcrest                    : '2.2',
        mockito                     : '5.6.0',
        powermock                   : '2.0.9',
        robolectric                 : '4.10.3',
        base64                      : '2.3.9'
    ]
}

allprojects {
    buildscript {
        repositories {
            google()
            mavenCentral()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:7.4.2'
        }
    }

    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

subprojects {
    afterEvaluate {project ->
        dependencies {
            testImplementation "junit:junit:${testLibraries.junit}"
            testImplementation "org.hamcrest:hamcrest-core:${testLibraries.hamcrest}"
            testImplementation "org.hamcrest:hamcrest-library:${testLibraries.hamcrest}"
            testImplementation "org.mockito:mockito-core:${testLibraries.mockito}"
            testImplementation "org.powermock:powermock-module-junit4:${testLibraries.powermock}"
            testImplementation "org.powermock:powermock-module-junit4-rule:${testLibraries.powermock}"
            testImplementation "org.powermock:powermock-api-mockito2:${testLibraries.powermock}"
            testImplementation "org.robolectric:robolectric:${testLibraries.robolectric}"
        }

        if (project.plugins.hasPlugin('android') || project.plugins.hasPlugin('android-library')) {
            android {
                defaultConfig {
                    compileSdk 32
                    minSdkVersion 30
                    targetSdkVersion 30

                    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_1_8
                    targetCompatibility = JavaVersion.VERSION_1_8
                }

                packagingOptions {
                    exclude 'META-INF/DEPENDENCIES'
                    exclude 'META-INF/NOTICE'
                    exclude 'META-INF/LICENSE'
                    exclude 'META-INF/LICENSE.txt'
                    exclude 'META-INF/NOTICE.txt'
                    exclude 'META-INF/rxjava.properties'
                }

                testOptions.unitTests.includeAndroidResources = true

                testOptions {
                    unitTests.all {
                        testLogging {
                            events "passed", "skipped", "failed", "standardError"
                            outputs.upToDateWhen {false}
                        }
                    }
                }
            }
        }
    }
}
